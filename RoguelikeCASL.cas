;**************************************
; プログラム開始
;----
;----
;**************************************
START		START
;----
;----
;======================================
; メインプログラム
;======================================
MAIN		NOP
			;--------------------------------------
			; 初期化処理
			;--------------------------------------
;----
			; プレイヤーのHP設定
			LD		GR0,PLYHPF
			ST		GR0,PLYHP
;----
			; 00ステートを設定
			LAD		GR1,0
			LD		GR1,CHNGTBL,GR1
			CALL	0,GR1
;----
			; 画面の初期化
			CALL	CLRSBUF
			CALL	MEZDRW
			CALL	DRWPLY
			CALL	DRWSBUF
;----
			;======================================
			; メインプループ
			;======================================
MAINL		NOP
;----
			CALL	INPUT		; 入力処理
;----
			;--------------------------------------
			; 終了判定
			;--------------------------------------
			LD		GR0,INBUF
			CPA		GR0,='E'
			JZE		MAINED
;----
; !!!!!!!!!!!!!! デバッグ更新処理 !!!!!!!!!!!!!!
			CALL	UPDDBG
;----
			; 更新処理
			LD		GR1,STATE
			LD		GR1,UPDTBL,GR1
			CALL	0,GR1
;----
			; 描画処理
			LD		GR1,STATE
			LD		GR1,DRWTBL,GR1
			CALL	0,GR1
;----
			;======================================
			; メインプループ終了
			;======================================
			JUMP	MAINL
;----
MAINED		RET
;----
;----
;----
;======================================
; メインプログラムデータ
;======================================
; ステート
STATE		DS	1			; メインステート
STATESUB	DS	1			; サブステート
; ステート変更テーブル
CHNGTBL		DC	CHNG00,CHNG01
; 更新テーブル
UPDTBL		DC	UPD00,UPD01
; 描画テーブル
DRWTBL		DC	DRW00,DRW01
;----
; 入力バッファ
INBUF		DS	8
INBUFL		DS	1
; ワークバッファ
WORKBUF		DS	10
;----
; プレイヤー情報
PLYX		DS	1
PLYY		DS	1
PLYHP		DS	1
PLYHPF		DC	20			; HP初期値
PLYHPD		DC	3			; HP表示桁
PLYHPX0		DC	32			; HP表示X座標
PLYHPX1		DC	35			; HP表示X座標
PLYHPY0		DC	2			; HP表示Y座標
PLYHPY1		DC	2			; HP表示Y座標
SCRHP		DC	'HP:'
;----
; マップ情報
ELVX		DS	1			; 階段X座標
ELVY		DS	1			; 階段Y座標
;----
; 文字キャラクタの定義
PCHR		DC	'@'		; プレイヤーキャラクタ
WCHR		DC	'#'		; 壁キャラクタ
ECHR		DC	'z'		; 階段キャラクタ
CCHR		DC	' '		; 空白キャラ
;----
; 画面バッファ
SCRL00		DS	40
SCRL01		DS	40
SCRL02		DS	40
SCRL03		DS	40
SCRL04		DS	40
SCRL05		DS	40
SCRL06		DS	40
SCRL07		DS	40
SCRL08		DS	40
SCRL09		DS	40
SCRL10		DS	40
SCRL11		DS	40
SCRL12		DS	40
SCRL13		DS	40
SCRL14		DS	40
SCRL15		DS	40
SCRL16		DS	40
SCRBUF		DC	SCRL00,SCRL01,SCRL02,SCRL03,SCRL04
SCRBUF1		DC	SCRL05,SCRL06,SCRL07,SCRL08,SCRL09
SCRBUF2		DC	SCRL10,SCRL11,SCRL12,SCRL13,SCRL14
SCRBUF3		DC	SCRL15,SCRL16
;----
LINELEN		DC	40		; 画面ラインの長さ
LINENUM		DC	17		; 画面ラインの数
;----
SCRMSGX		DC	2		; メッセージX座標
SCRMSGY1	DC	14		; メッセージY1座標
SCRMSGY2	DC	15		; メッセージY2座標
MSGLEN		DC	38		; メッセージの長さ
;----
;======================================
; 入力処理
;======================================
INPUT		IN		INBUF,INBUFL
			RET
;----
;----
;======================================
; デバッグ処理
;======================================
UPDDBG		LD		GR0,INBUFL
			JZE		UPDDBGE
;----
			LD		GR0,INBUF
			CPA		GR0,='a'
			JZE		UPDDBGAA
			JUMP	UPDDBGE
;----
			; プレイヤーを階段に移動
UPDDBGAA	LD		GR0,ELVX
			ST		GR0,PLYX
			LD		GR0,ELVY
			ST		GR0,PLYY
			JUMP	UPDDBGE
;----
UPDDBGE		RET
;----
;----
;======================================
; 00迷路移動中の処理
;======================================
;--------------------------------------
; 00変更
;--------------------------------------
CHNG00		CALL	MEZCRT			; 迷路の生成
			CALL	SETELV			; 階段を設定
			CALL	SETPLY			; プレイヤーの設定
			LAD		GR0,0			; ステートを00に変更
			ST		GR0,STATE
			RET
;----
;----
;--------------------------------------
; 00更新
;--------------------------------------
UPD00		CALL	UPDPLY
			CALL	CHKELV
			; 階段を降りていたらステートを変更
			OR		GR0,GR0
			JZE		UPD00ED
;----
BRK			CALL	CHNG01
;----
UPD00ED		RET
;----
;----
;--------------------------------------
; 00描画
;--------------------------------------
DRW00		CALL	CLRSBUF			; 画面バッファのクリア
			CALL	MEZDRW			; 迷路の描画
			CALL	DRWPLY			; プレイヤーの描画
			CALL	DRWSBUF			; 画面バッファの描画
			RET
;----
;----
;----
;----
;======================================
; 01階段降り中へ処理
;======================================
;--------------------------------------
; 01変更
;--------------------------------------
CHNG01		NOP
			; ステートを01に変更
			LAD		GR0,1
			ST		GR0,STATE
			LAD		GR0,0
			ST		GR0,STATESUB
			RET
;----
;--------------------------------------
; 01更新
;--------------------------------------
UPD01		NOP
			; サブステートを加算
			LD		GR1,STATESUB
			LAD		GR1,1,GR1
			ST		GR1,STATESUB
			CPA		GR1,=3
			JNZ		UPD01E
;----
			CALL	CHNG00
;----
UPD01E		RET
;----
;--------------------------------------
; 01描画
;--------------------------------------
DRW01		CALL	CLRSBUF			; 画面バッファのクリア
;			CALL	MEZDRW			; 迷路の描画
;----
			; サブステート事にメッセージを表示
BRK3		LD		GR2,STATESUB
			LD		GR1,MSG01,GR2
			LD		GR4,MSG01L,GR2
			CALL	CPYMSG1
;----
			CALL	DRWSBUF			; 画面バッファの描画
BRK2		RET
;----
MSG010		DC	'階段を下りた'
MSG011		DC	'階段を下りた…'
MSG012		DC	'階段を下りた……'
MSG01		DC	MSG010,MSG011,MSG012
MSG01L		DC	12,14,16
;----
;----
;----
;----
;======================================
; 画面バッファのクリア
;======================================
CLRSBUF		NOP
;			LD		GR4,CCHR
			LD		GR4,='-'
			LAD		GR5,0
;----
		; 画面ラインの数分ループ		
			LAD		GR1,0
CLRSBL0		NOP
;----
		; 画面ラインの長さ分ループ		
			LAD		GR2,0
CLRSBL1		ST		GR4,SCRL00,GR5
			LAD		GR5,1,GR5
			LAD		GR2,1,GR2
			CPA		GR2,LINELEN
			JNZ		CLRSBL1
;----
			LAD		GR1,1,GR1
			CPA		GR1,LINENUM
			JNZ		CLRSBL0
			RET
;----
;----
;======================================
; 画面バッファへコピー
; input : GR1  アドレス  壊す
;         GR2  X座標     壊す
;         GR3  Y座標     壊す
;         GR4  幅        壊す
;         GR5  高さ      壊す
;======================================
CPYSBUF		NOP
;----
CPYSBUF0	LD		GR7,SCRBUF,GR3
			ADDL	GR7,GR2
;----
CPYSBUF1	LD		GR6,0,GR1
			ST		GR6,0,GR7
			LAD		GR7,1,GR7
			LAD		GR1,1,GR1
;----
			SUBA	GR4,=1
			JNZ		CPYSBUF1
;----
			LAD		GR3,1,GR3
			SUBA	GR5,=1
			JNZ		CPYSBUF0
			RET
;----
;----
;======================================
; メッセージ領域1へコピー
; input : GR1  アドレス  壊す
;         GR4  幅        壊す
;======================================
CPYMSG1		LD		GR2,SCRMSGX
			LD		GR3,SCRMSGY1
			LAD		GR5,1
			CALL	CPYSBUF
			RET
;----
;----
;======================================
; メッセージ領域2へコピー
; input : GR1  アドレス  壊す
;         GR4  幅        壊す
;======================================
CPYMSG2		LD		GR2,SCRMSGX
			LD		GR3,SCRMSGY2
			LAD		GR5,1
			CALL	CPYSBUF
			RET
;----
;----
;======================================
; 画面バッファの描画
;======================================
DRWSBUF		OUT		CCHR,NUM1
			OUT		CCHR,NUM1
			OUT		SCRL00,LINELEN
			OUT		SCRL01,LINELEN
			OUT		SCRL02,LINELEN
			OUT		SCRL03,LINELEN
			OUT		SCRL04,LINELEN
			OUT		SCRL05,LINELEN
			OUT		SCRL06,LINELEN
			OUT		SCRL07,LINELEN
			OUT		SCRL08,LINELEN
			OUT		SCRL09,LINELEN
			OUT		SCRL10,LINELEN
			OUT		SCRL11,LINELEN
			OUT		SCRL12,LINELEN
			OUT		SCRL13,LINELEN
			OUT		SCRL14,LINELEN
			OUT		SCRL15,LINELEN
			OUT		SCRL16,LINELEN
			OUT		CCHR,NUM1
			OUT		CCHR,NUM1
			RET
;----
NUM1		DC	1
;----
;----
;======================================
; 階段の設定
;======================================
SETELV		LD		GR7,ECHR
			CALL	MEZRDPOK
			ST		GR5,ELVX
			ST		GR6,ELVY
			RET
;----
;----
;======================================
; 階段の判定
; return :  GR0  0:階段なし  1:階段降り
;======================================
CHKELV		LAD		GR0,0
			LD		GR1,INBUFL
			JZE		CHKELVED
;----
			LD		GR1,INBUF
			CPA		GR1,='5'
			JNZ		CHKELVED
;----
			LD		GR5,PLYX
			LD		GR6,PLYY
			CALL	MEZPEEK
			CPA		GR7,ECHR
			JNZ		CHKELVED
;----
			LAD		GR0,1
;----
CHKELVED	RET
;----
;----
;======================================
; プレイヤー座標の設定
;======================================
SETPLY		NOP
			; 乱数を取得
SETPLYL		CALL	RND
			LD		GR5,GR7
			CALL	RND
			LD		GR6,GR7
			; 乱数の範囲を制限
			AND		GR5,PLYMSK
			AND		GR6,PLYMSK
			; 乱数にオフセットを加える
			ADDA	GR5,PLYOFFX
			ADDA	GR6,PLYOFFY
			; 迷路のデータを取得し壁だったら
			; もう一度ランダムを取得
			CALL	MEZPEEK
			CPA		GR7,WCHR
			JZE		SETPLYL
			; 座標を設定
			ST		GR5,PLYX
			ST		GR6,PLYY
			RET
;----
PLYMSK		DC	#0007
PLYOFFX		DC	4
PLYOFFY		DC	2
;----
;----
;======================================
; プレイヤーの更新
;======================================
UPDPLY		LD		GR0,INBUFL
			JZE		UPDPLYED
			LD		GR0,INBUF
			LD		GR5,PLYX
			LD		GR6,PLYY
			; 入力によって移動方向を変える
			CPA		GR0,='4'
			JZE		UPDPLY4
			CPA		GR0,='6'
			JZE		UPDPLY6
			CPA		GR0,='8'
			JZE		UPDPLY8
			CPA		GR0,='2'
			JZE		UPDPLY2
			JUMP	UPDPLYED
;----
UPDPLY4		SUBA	GR5,=1
			JUMP	UPDPLYCH
;----
UPDPLY6		ADDA	GR5,=1
			JUMP	UPDPLYCH
;----
UPDPLY8		SUBA	GR6,=1
			JUMP	UPDPLYCH
;----
UPDPLY2		ADDA	GR6,=1
			JUMP	UPDPLYCH
;----
			; 移動先が壁だったら座標を設定しない
UPDPLYCH	CALL	MEZPEEK
			CPA		GR7,WCHR
			JZE		UPDPLYED
			ST		GR5,PLYX
			ST		GR6,PLYY
;----
UPDPLYED	RET
;----
;----
;======================================
; プレイヤーの描画
;======================================
DRWPLY		NOP
			; プレイヤーを画面に表示
			LD		GR1,PLYY
			LD		GR2,SCRBUF,GR1
			ADDL	GR2,PLYX
			LD		GR0,PCHR
			ST		GR0,0,GR2
;----
			; HPを文字列に変換
			LD		GR0,PLYHP
			LD		GR1,PLYHPD
			LAD		GR2,WORKBUF
			LAD		GR3,0
			CALL	DECCNV
;----
			; 'HP:'の表示
			LAD		GR1,SCRHP
			LD		GR2,PLYHPX0
			LD		GR3,PLYHPY0
			LD		GR4,PLYHPD
			LAD		GR5,1
			CALL	CPYSBUF
;----
			; HPの表示
			LAD		GR1,WORKBUF
			LD		GR2,PLYHPX1
			LD		GR3,PLYHPY1
			LD		GR4,PLYHPD
			LAD		GR5,1
			CALL	CPYSBUF
			RET
;----
;----
;**************************************
; 迷路プログラム
;----
;----
;**************************************
;----
;----
;======================================
; 迷路生成
;======================================
MEZCRT		CALL	MEZCLR
;----
			LD		GR0,WCHR
;----
			; 外壁を埋める
			; 上下壁
			LD		GR1,MEZLLEN
MEZCRTL0	SUBL	GR1,=1
			ST		GR0,MEZL00,GR1
			ST		GR0,MEZL12,GR1
			JNZ		MEZCRTL0
;----
			; 左右壁
			LD		GR1,MEZLNUM
			LAD		GR2,0
MEZCRTL1	ST		GR0,MEZL00,GR2
			ST		GR0,MEZL00R,GR2
			ADDL	GR2,MEZLLEN
			SUBL	GR1,=1
			JNZ		MEZCRTL1
;----
			; 棒倒し法で壁を生成
;----
			;----------------
			; 一段目は上も倒す
			LD		GR1,MEZSTR0
MEZCRTL2	NOP
			; 既に壁があったら処理を飛ばす
			LD		GR2,0,GR1
			CPA		GR2,CCHR
			JNZ		MEZCRNX0
;----
			; 壁を設定
			ST		GR0,0,GR1
;----
			; 壁の無いマスが出るまでランダムを取得する
RNDL0		CALL	RND
			AND		GR7,=#0003
			LD		GR7,PICH,GR7
			ADDA	GR7,GR1
			LD		GR2,0,GR7
			CPA		GR2,CCHR
			JNZ		RNDL0
;----
			; 棒倒し壁を設定
			ST		GR0,0,GR7
;----
MEZCRNX0	LAD		GR1,2,GR1
			CPA		GR1,MEZEND0
			JMI		MEZCRTL2
;----
;----
			;----------------
			; 二段目以降は上以外に壁を設定
			LD		GR1,MEZSTR1
MEZCRTL3	LD		GR2,0
MEZCRTL4	NOP
;----
			; 既に壁があったら処理を飛ばす
			LD		GR3,0,GR1
			CPA		GR3,CCHR
			JNZ		MEZCRNX1
;----
			; 壁を設定
			ST		GR0,0,GR1
;----
			; 壁が無くて上以外のマスが出るまでランダムを取得する
RNDL1		CALL	RND
			; 上ならもう一度ランダム取得
			AND		GR7,=#0003
			JZE		RNDL1
			; 既に壁があったらもう一度ランダム取得
			LD		GR7,PICH,GR7
			ADDA	GR7,GR1
			LD		GR3,0,GR7
			CPA		GR3,CCHR
			JNZ		RNDL1
;----
			; 棒倒し壁を設定
			ST		GR0,0,GR7
;----
MEZCRNX1	LAD		GR1,2,GR1
			LAD		GR2,2,GR2
			CPA		GR2,MEZLLEN
			JMI		MEZCRTL4
;----
			ADDA	GR1,MEZLLEN
			LAD		GR1,-1,GR1
;			AND		GR1,=#FFFE
			CPA		GR1,MEZEND1
			JMI		MEZCRTL3
;----
;----
			RET
;----
PICH		DC	 -31,-1,1,31
;----
;----
;======================================
; 迷路クリア
;======================================
MEZCLR		LD		GR0,CCHR
			LD		GR1,MEZDATSZ
MEZCLRL		SUBL	GR1,=1
			ST		GR0,MEZL00,GR1
			JNZ		MEZCLRL
			RET
MEZCLRD		DC	' '
;----
;----
;======================================
; 迷路描画
;======================================
MEZDRW		LAD		GR1,0
;----
MEZDRWL0	LAD		GR2,0
			LD		GR3,MEZDAT,GR1
			LD		GR4,SCRBUF,GR1
;----
MEZDRWL1	LD		GR0,0,GR3
			ST		GR0,0,GR4
;----
			LAD		GR3,1,GR3
			LAD		GR4,1,GR4
			LAD		GR2,1,GR2
			CPA		GR2,MEZLLEN
			JMI		MEZDRWL1
;----
			LAD		GR1,1,GR1
			CPA		GR1,MEZLNUM
			JMI		MEZDRWL0
			RET
;----
;----
;======================================
; 迷路のデータを取得
; input  : GR5  X座標
;          GR6  Y座標
; return : GR7  データ
;======================================
MEZPEEK		LD		GR7,MEZDAT,GR6
			ADDA	GR7,GR5
			LD		GR7,0,GR7
			RET
;----
;----
;======================================
; 迷路のデータを設定
; input  : GR5  X座標
;          GR6  Y座標
;          GR7  データ
; work   : GR4
;======================================
MEZPOKE		LD		GR4,MEZDAT,GR6
			ADDA	GR4,GR5
			ST		GR7,0,GR4
			RET
;----
;----
;======================================
; 迷路のデータをランダム設定
; input  : GR7  データ
; return : GR5  X座標
;          GR6  Y座標
;======================================
MEZRDPOK	PUSH	0,GR7
			; 乱数を取得
MEZRDPOL	CALL	RND
			LD		GR5,GR7
			CALL	RND
			LD		GR6,GR7
			; 乱数の範囲を制限
			AND		GR5,=#001F
			AND		GR6,=#000F
			; 範囲外だったらもう一度ランダムを取得
			CPA		GR5,MEZLLEN
			JPL		MEZRDPOL
			JZE		MEZRDPOL
			CPA		GR6,MEZLNUM
			JPL		MEZRDPOL
			JZE		MEZRDPOL
			; 迷路のデータを取得し壁だったら
			; もう一度ランダムを取得
			CALL	MEZPEEK
			CPA		GR7,CCHR
			JNZ		MEZRDPOL
			; 座標を設定
			POP		GR7
			CALL	MEZPOKE
			RET
;----
;----
;======================================
; 迷路データ
;======================================
MEZL00		DS	30		; 上壁,左壁開始
MEZL00R		DS	1		; 右壁開始
MEZL01		DS	31		; 空白行
MEZL02		DS	31		; 1段目柱行
MEZL03		DS	31		; 空白行
MEZL04		DS	31		; 2段目柱行
MEZL05		DS	31		; 空白行
MEZL06		DS	31		; 柱行
MEZL07		DS	31		; 空白行
MEZL08		DS	31		; 柱行
MEZL09		DS	31		; 空白行
MEZL10		DS	31		; 柱行
MEZL11		DS	31		; 空白行
MEZL12		DS	31		; 下壁
MEZDAT		DC	MEZL00,MEZL01,MEZL02,MEZL03,MEZL04
MEZDAT1		DC	MEZL05,MEZL06,MEZL07,MEZL08,MEZL09
MEZDAT2		DC	MEZL10,MEZL11,MEZL12
;----
MEZLLEN		DC	31
MEZLNUM		DC	13
MEZDATSZ	DC	403
;----
; 一段目の開始アドレスと終了アドレス
MEZSTR0		DC	MEZL02
MEZEND0		DC	MEZL03
;----
; 二段目以降の開始アドレスと終了アドレス
MEZSTR1		DC	MEZL04
MEZEND1		DC	MEZL11
;----
;----
;----
;----
;**************************************
; ユーティリティモジュール
;----
;----
;**************************************
;----
;----
;======================================
; 10進数変換関数
; input  : GR0   数値
;        : GR1   桁数 最大5桁
;        : GR2   格納アドレス
;        : GR3   0:ゼロサプレス,1:ゼロ埋め
;======================================
DECCNV		NOP
			; 桁数を退避
			PUSH	0,GR1
			; 格納アドレスを退避
			PUSH	0,GR2
			; 桁数から処理回数を設定
			LD		GR6,MAXDIGIT
			SUBL	GR6,GR1
			; 数値を初期化
			LD		GR4,GR0
;----
DECCNV0		LAD		GR5,0
DECCNV1		CPL		GR4,DIGIT,GR6
			JMI		DECCNV1E
;----
			SUBL	GR4,DIGIT,GR6
			LAD		GR5,1,GR5
			JUMP	DECCNV1
;----
DECCNV1E	LD		GR7,NUMCHR,GR5
			ST		GR7,0,GR2
			LAD		GR2,1,GR2
			LAD		GR6,1,GR6
;----
			CPL		GR6,MAXDIGIT
			JNZ		DECCNV0
;----
			; ゼロサプレス処理
			POP		GR2
			POP		GR1
			OR		GR3,GR3
			JNZ		DECCNVED
;----
DECCNV2		LD		GR7,0,GR2
			CPA		GR7,NUMCHR
			JNZ		DECCNVED
;----
			SUBL	GR1,=1
			JZE		DECCNVED
;----
			LD		GR6,PRSSCHR
			ST		GR6,0,GR2
			LAD		GR2,1,GR2
			JUMP	DECCNV2
;----
DECCNVED	RET
;----
;----
PRSSCHR		DC	' '
NUMCHR		DC	'0','1','2','3','4'
NUMCHR2		DC	'5','6','7','8','9'
DIGIT		DC	10000,1000,100,10,1
MAXDIGIT	DC	5
DIGLOPNM	DC	4
;----
;----
;======================================
; ランダム関数
; input  : なし
; return : GR7   乱数
; work   : GR6
;======================================
RND			LD		GR7,SEED
			LD		GR6,GR7
			SLL		GR7,2
			ADDL	GR7,GR6
			ADDL	GR7,=#3711
			ST		GR7,SEED
			SRL		GR7,4
			AND		GR7,=#00FF
			RET
;----
SEED		DC	#3643
;----
;----
;----
;----
;**************************************
; プログラム終了
;----
;----
;**************************************
			END
