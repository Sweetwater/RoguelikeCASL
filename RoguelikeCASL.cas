;**************************************
; プログラム開始
;----
;----
START		START
;----
;======================================
; メインプログラム
;======================================
MAIN		NOP
			;--------------------------------------
			; 初期化処理
			;--------------------------------------
;----
			; お金の初期化
			LAD		GR0,0
			ST		GR0,PLYMNY
;----
			; 階数の初期化
			LAD		GR0,1
			ST		GR0,ELVNUM
;----
			; プレイヤーのHP設定
			LD		GR0,PLYHPF
			ST		GR0,PLYHP
;----
			; 00ステートを設定
			LAD		GR1,0
			LD		GR1,CHNGTBL,GR1
			CALL	0,GR1
;----
			; 初期画面の描画
			LD		GR1,STATE
			LD		GR1,DRWTBL,GR1
			CALL	0,GR1
;----
			;======================================
			; メインプループ
			;======================================
MAINL		NOP
;----
			CALL	INPUT		; 入力処理
;----
			;--------------------------------------
			; 終了判定
			;--------------------------------------
			LD		GR0,INBUF
			CPA		GR0,='E'
			JZE		MAINED
;----
; !!!!!!!!!!!!!! デバッグ更新処理 !!!!!!!!!!!!!!
;			CALL	DBGUPD
;----
			; 更新処理
			LD		GR1,STATE
			LD		GR1,UPDTBL,GR1
			CALL	0,GR1
;----
			; 描画処理
			LD		GR1,STATE
			LD		GR1,DRWTBL,GR1
			CALL	0,GR1
;----
			;======================================
			; メインプループ終了
			;======================================
			JUMP	MAINL
;----
MAINED		RET
;----
;----
;**************************************
; メインプログラムデータ
;----
;----
; ステート
STATE		DS	1			; メインステート
STATESUB	DS	1			; サブステート
; ステート変更テーブル
CHNGTBL		DC	CHNG00,CHNG01
; 更新テーブル
UPDTBL		DC	UPD00,UPD01
; 描画テーブル
DRWTBL		DC	DRW00,DRW01
;----
; 入力バッファ
INBUF		DS	8
INBUFL		DS	1
; ワークバッファ
WORKBUF		DS	64
;----
; プレイヤー情報
PLYX		DS	1
PLYY		DS	1
PLYHP		DS	1
PLYHPF		DC	20			; HP初期値
PLYD		DS	1			; ダメージ
PLYTRGE		DS	1			; 敵ターゲット
PLYTRGED	DS	1			; 敵ターゲットダメージ
PLYMNY		DS	1			; お金
PLYFMNY		DS	1			; フレームのお金
PLYCUR		DS	1			; 回復
;----
; 階数情報
ELVNUM		DS	1
;----
; マップ情報
ELVX		DS	1			; 階段X座標
ELVY		DS	1			; 階段Y座標
;----
; 文字キャラクタの定義
CHRPLY		DC	'>'			; プレイヤーキャラクタ
CHRWAL		DC	'#'			; 壁キャラクタ
CHRELV		DC	'='			; 階段キャラクタ
CHRMNY		DC	'$'			; お金キャラクタ
CHRCUR		DC	'+'			; 回復キャラクタ
CLRCHR		DC	' '			; 空白キャラ
;----
; マップデータ
MSKWAL		DC	#8000		; 壁
MSKPLY		DC	#0100		; プレイヤー
MSKEMY		DC	#00F0		; 敵
MSKITM		DC	#000F		; アイテム
MSKMNY		DC	#0001		; お金
MSKCUR		DC	#0002		; 回復
MSKELV		DC	#0008		; 階段
MSKCLO		DC	#FFF0		; 通行止めか
MAPDAT00	DS	32
MAPDAT01	DS	32
MAPDAT02	DS	32
MAPDAT03	DS	32
MAPDAT04	DS	32
MAPDAT05	DS	32
MAPDAT06	DS	32
MAPDAT07	DS	32
MAPDAT08	DS	32
MAPDAT09	DS	32
MAPDAT10	DS	32
MAPDAT11	DS	32
MAPDAT12	DS	32
MAPDAT13	DS	32
MAPDAT14	DS	32
MAPDAT15	DS	32
MAPDATA		DC	MAPDAT00,MAPDAT01,MAPDAT02,MAPDAT03
MAPDATA1	DC	MAPDAT04,MAPDAT05,MAPDAT06,MAPDAT07
MAPDATA2	DC	MAPDAT08,MAPDAT09,MAPDAT10,MAPDAT11
MAPDATA3	DC	MAPDAT12,MAPDAT13,MAPDAT14,MAPDAT15
MAPDATL		DC	32
MAPDATNM	DC	16
;----
; 画面バッファ
SCRL00		DS	40
SCRL01		DS	40
SCRL02		DS	40
SCRL03		DS	40
SCRL04		DS	40
SCRL05		DS	40
SCRL06		DS	40
SCRL07		DS	40
SCRL08		DS	40
SCRL09		DS	40
SCRL10		DS	40
SCRL11		DS	40
SCRL12		DS	40
SCRL13		DS	40
SCRL14		DS	40
SCRL15		DS	40
SCRL16		DS	40
SCRBUF		DC	SCRL00,SCRL01,SCRL02,SCRL03,SCRL04
SCRBUF1		DC	SCRL05,SCRL06,SCRL07,SCRL08,SCRL09
SCRBUF2		DC	SCRL10,SCRL11,SCRL12,SCRL13,SCRL14
SCRBUF3		DC	SCRL15,SCRL16
;----
LINELEN		DC	40		; 画面ラインの長さ
LINENUM		DC	17		; 画面ラインの数
;----
SCRMSGX		DC	2		; メッセージX座標
SCRMSGY1	DC	14		; メッセージY1座標
SCRMSGY2	DC	15		; メッセージY2座標
SCRMSGL		DC	38		; メッセージの長さ
;----
;----
;**************************************
; 入力処理
;----
;----
INPUT		IN		INBUF,INBUFL
			RET
;----
;----
;**************************************
; デバッグ処理
;----
;----
DBGUPD		LD		GR0,INBUFL
			JZE		DBGUPDE
;----
			LD		GR0,INBUF
			CPA		GR0,='a'
			JZE		DBGUPDAA
			JUMP	DBGUPDE
;----
			; プレイヤーを階段に移動
DBGUPDAA	LD		GR0,ELVX
			ST		GR0,PLYX
			LD		GR0,ELVY
			ST		GR0,PLYY
			JUMP	DBGUPDE
;----
DBGUPDE		RET
;----
;----
;**************************************
; 00迷路移動中の処理
;----
;----
;--------------------------------------
; 00変更
;--------------------------------------
CHNG00		CALL	MEZCRT			; 迷路の生成
			CALL	MAPCLR			; マップデータをクリア
			CALL	MAPMEZ			; 迷路データをマップデータに設定
			CALL	ELVSET			; 階段を設定
			CALL	EMYSET			; 敵を設定
			CALL	CURSET			; 回復を設定
			CALL	MNYSET			; お金を設定
			CALL	PLYSET			; プレイヤーの設定
			LAD		GR0,0			; ステートを00に変更
			ST		GR0,STATE
			RET
;----
;----
;--------------------------------------
; 00更新
;--------------------------------------
UPD00		LAD		GR0,0
			ST		GR0,PLYD
			ST		GR0,PLYTRGED
			ST		GR0,PLYFMNY
			ST		GR0,PLYCUR
			CALL	PLYUPD
			CALL	EMYUPD
			CALL	MNYCHK
			CALL	CURCHK
			CALL	ELVCHK
			; 階段を降りていたらステートを変更
			OR		GR0,GR0
			JZE		UPD00ED
;----
			CALL	CHNG01
;----
UPD00ED		RET
;----
;----
;--------------------------------------
; 00描画
;--------------------------------------
DRW00		CALL	SBUFCLR			; 画面バッファのクリア
			CALL	MEZDRW			; 迷路の描画
			CALL	HUDDRW			; HUDの描画
			CALL	ELVDRW			; 階段の描画
			CALL	MNYDRW			; お金の描画
			CALL	CURDRW			; 回復の描画
			CALL	EMYDRW			; 敵の描画
			CALL	PLYDRW			; プレイヤーの描画
			CALL	DRW00MSG		; メッセージ描画
			CALL	DRWSBUF			; 画面バッファの描画
			RET
;----
;----
;--------------------------------------
; 00メッセージ描画
;--------------------------------------
DRW00MSG	NOP
;----
			; 敵にダメージが無かったら表示しない
			LD		GR0,PLYTRGED
			JZE		DRW00MS0
			JMI		DRW00MS2
;----
			; XXXXXXにダメージ与えた表示
			; テンプレート文字列をワークバッファにコピー
			LD		GR0,MSG000L
			LAD		GR1,MSG000
			LAD		GR2,WORKBUF
			CALL	CPYDAT
;----
			; 敵の名前をコピー
			LD		GR0,MSG00EL
			LD		GR1,PLYTRGE
			LD		GR1,MSG00E,GR1
			LAD		GR2,WORKBUF
			CALL	CPYDAT
;----
			; ダメージを設定
			LD		GR0,PLYTRGED
			LD		GR1,=3
			LD		GR2,MSG000OF
			LAD		GR2,WORKBUF,GR2
			LAD		GR3,0
			CALL	DECCNV
;----
			; 文字列を表示
			LAD		GR1,WORKBUF
			LD		GR4,MSG000L
			CALL	CPYMSG1
			JUMP	DRW00MS0
;----
;----
			; XXXXXXを倒した表示
			; テンプレート文字列をワークバッファにコピー
DRW00MS2	LD		GR0,MSG002L
			LAD		GR1,MSG002
			LAD		GR2,WORKBUF
			CALL	CPYDAT
;----
			; 敵の名前をコピー
			LD		GR0,MSG00EL
			LD		GR1,PLYTRGE
			LD		GR1,MSG00E,GR1
			LAD		GR2,WORKBUF
			CALL	CPYDAT
;----
			; 文字列を表示
			LAD		GR1,WORKBUF
			LD		GR4,MSG002L
			CALL	CPYMSG1
;----
;----
			; プレイヤーにダメージが無かったら表示しない
DRW00MS0	LD		GR0,PLYD
			JZE		DRW00MS1
			JMI		DRW00MS3
;----
			; ダメージ受けた表示
			; テンプレート文字列をワークバッファにコピー
			LD		GR0,MSG001L
			LAD		GR1,MSG001
			LAD		GR2,WORKBUF
			CALL	CPYDAT
;----
			; ダメージを設定
			LD		GR0,PLYD
			LD		GR1,=3
			LD		GR2,MSG001OF
			LAD		GR2,WORKBUF,GR2
			LAD		GR3,0
			CALL	DECCNV
;----
			; 文字列を表示
			LAD		GR1,WORKBUF
			LD		GR4,MSG001L
			CALL	CPYMSG2
			JUMP	DRW00MS1
;----
			; 倒された表示
			; 文字列を表示
DRW00MS3	LAD		GR1,MSG003
			LD		GR4,MSG003L
			CALL	CPYMSG2
;----
			; 回復を表示
DRW00MS1	LD		GR1,PLYCUR
			JZE		DRW00MS4
;----
			; テンプレート文字列をワークバッファにコピー
			LD		GR0,MSG004L
			LAD		GR1,MSG004
			LAD		GR2,WORKBUF
			CALL	CPYDAT
;----
			; 回復を設定
			LD		GR0,PLYCUR
			LD		GR1,=3
			LD		GR2,MSG004OF
			LAD		GR2,WORKBUF,GR2
			LAD		GR3,0
			CALL	DECCNV
;----
			; 文字列を表示
			LAD		GR1,WORKBUF
			LD		GR4,MSG004L
			CALL	CPYMSG1
;----
			; お金を表示
DRW00MS4	LD		GR1,PLYFMNY
			JZE		DRW00MS5
;----
			; 文字列を表示
			LAD		GR1,MSG005
			LD		GR4,MSG005L
			CALL	CPYMSG1
;----
DRW00MS5	RET
;----
;----
MSG000		DC	'          に   のダメージを与えた'
MSG000L		DC	33
MSG000OF	DC	12
;----
MSG001		DC	'敵から   のダメージを受けた'
MSG001L		DC	27
MSG001OF	DC	6
;----
MSG002		DC	'          を倒した…'
MSG002L		DC	20
;----
MSG003		DC	'敵に倒された……'
MSG003L		DC	16
;----
MSG00E0		DC	'アルファ  '
MSG00E1		DC	'ブラボー  '
MSG00E2		DC	'チャーリー'
MSG00E3		DC	'デルタ    '
MSG00E4		DC	'エコー    '
MSG00E5		DC	'フォックス'
MSG00E6		DC	'ゴルフ    '
MSG00E7		DC	'ホテル    '
MSG00E8		DC	'インディア'
MSG00E9		DC	'ジュリエト'
MSG00E		DC	MSG00E0,MSG00E1,MSG00E2,MSG00E3,MSG00E4
MSG00ET2	DC	MSG00E5,MSG00E6,MSG00E7,MSG00E8,MSG00E9
MSG00EL		DC	10
;----
MSG004		DC	'HPが   回復した!!'
MSG004L		DC	17
MSG004OF	DC	4
;----
MSG005		DC	'お金を100G拾った………'
MSG005L		DC	22
;----
;----
;**************************************
; 01階段降り中へ処理
;----
;----
;--------------------------------------
; 01変更
;--------------------------------------
CHNG01		LAD		GR0,1
			ST		GR0,STATE
			LAD		GR0,0
			ST		GR0,STATESUB
			RET
;----
;----
;--------------------------------------
; 01更新
;--------------------------------------
UPD01		LD		GR1,STATESUB
			LAD		GR1,1,GR1
			ST		GR1,STATESUB
			CPA		GR1,=3
			JNZ		UPD01E
;----
			; 階数をインクリメント
			LD		GR1,ELVNUM
			LAD		GR1,1,GR1
			ST		GR1,ELVNUM
			CALL	CHNG00
;----
UPD01E		RET
;----
;----
;--------------------------------------
; 01描画
;--------------------------------------
DRW01		CALL	SBUFCLR			; 画面バッファのクリア
;----
			; サブステート事にメッセージを表示
			LD		GR2,STATESUB
			LD		GR1,MSG01,GR2
			LD		GR4,MSG01L,GR2
			CALL	CPYMSG1
;----
			CALL	DRWSBUF			; 画面バッファの描画
			RET
;----
MSG010		DC	'階段を下りた'
MSG011		DC	'階段を下りた…'
MSG012		DC	'階段を下りた……'
MSG01		DC	MSG010,MSG011,MSG012
MSG01L		DC	12,14,16
;----
;----
;**************************************
;  画面バッファプログラム
;----
;----
;======================================
; 画面バッファのクリア
;======================================
SBUFCLR		NOP
			LD		GR4,CLRCHR
;			LD		GR4,='-'
			LAD		GR5,0
;----
		; 画面ラインの数分ループ
			LAD		GR1,0
SBUFCLR0	NOP
;----
		; 画面ラインの長さ分ループ
			LAD		GR2,0
SBUFCLR1	ST		GR4,SCRL00,GR5
			LAD		GR5,1,GR5
			LAD		GR2,1,GR2
			CPA		GR2,LINELEN
			JNZ		SBUFCLR1
;----
			LAD		GR1,1,GR1
			CPA		GR1,LINENUM
			JNZ		SBUFCLR0
			RET
;----
;----
;======================================
; 画面バッファへコピー
; input : GR1  アドレス  壊す
;         GR2  X座標     壊す
;         GR3  Y座標     壊す
;         GR4  幅        壊す
;         GR5  高さ      壊す
;======================================
CPYSBUF		NOP
			LD		GR0,GR4
;----
CPYSBUF0	LD		GR7,SCRBUF,GR3
			ADDL	GR7,GR2
;----
CPYSBUF1	LD		GR6,0,GR1
			ST		GR6,0,GR7
			LAD		GR7,1,GR7
			LAD		GR1,1,GR1
;----
			SUBA	GR4,=1
			JNZ		CPYSBUF1
;----
			LD		GR4,GR0
			LAD		GR3,1,GR3
			SUBA	GR5,=1
			JNZ		CPYSBUF0
			RET
;----
;----
;======================================
; 画面バッファに一文字描画
; input :  GR0  文字
;          GR5  X座標
;          GR6  Y座標
;======================================
PUTSBUF		LD		GR7,SCRBUF,GR6
			ADDL	GR7,GR5
			ST		GR0,0,GR7
			RET
;----
;----
;======================================
; メッセージ領域1へコピー
; input : GR1  アドレス  壊す
;         GR4  幅        壊す
;======================================
CPYMSG1		LD		GR2,SCRMSGX
			LD		GR3,SCRMSGY1
			LAD		GR5,1
			CALL	CPYSBUF
			RET
;----
;----
;======================================
; メッセージ領域2へコピー
; input : GR1  アドレス  壊す
;         GR4  幅        壊す
;======================================
CPYMSG2		LD		GR2,SCRMSGX
			LD		GR3,SCRMSGY2
			LAD		GR5,1
			CALL	CPYSBUF
			RET
;----
;----
;======================================
; 画面バッファの描画
;======================================
DRWSBUF		OUT		CLRCHR,NUM1
			OUT		CLRCHR,NUM1
			OUT		SCRL00,LINELEN
			OUT		SCRL01,LINELEN
			OUT		SCRL02,LINELEN
			OUT		SCRL03,LINELEN
			OUT		SCRL04,LINELEN
			OUT		SCRL05,LINELEN
			OUT		SCRL06,LINELEN
			OUT		SCRL07,LINELEN
			OUT		SCRL08,LINELEN
			OUT		SCRL09,LINELEN
			OUT		SCRL10,LINELEN
			OUT		SCRL11,LINELEN
			OUT		SCRL12,LINELEN
			OUT		SCRL13,LINELEN
			OUT		SCRL14,LINELEN
			OUT		SCRL15,LINELEN
			OUT		SCRL16,LINELEN
			OUT		CLRCHR,NUM1
			OUT		CLRCHR,NUM1
			RET
;----
NUM1		DC	1
;----
;----
;**************************************
;  HUDプログラム
;----
;----
;======================================
;  HUDの描画
;======================================
HUDDRW		NOP
;----
			; 階数の表示
			LD		GR0,ELVNUM
			LAD		GR1,3
			LAD		GR2,WORKBUF
			LAD		GR3,0
			CALL	DECCNV
;----
			LAD		GR0,1
			LAD		GR1,HUDFLR
			LAD		GR2,3
			LAD		GR2,WORKBUF,GR2
			CALL	CPYDAT
;----
			LAD		GR1,WORKBUF
			LAD		GR2,36
			LAD		GR3,1
			LAD		GR4,4
			LAD		GR5,1
			CALL	CPYSBUF
;----
			; HPの表示
			LAD		GR0,3
			LAD		GR1,HUDHP
			LAD		GR2,WORKBUF
			CALL	CPYDAT
;----
			LD		GR0,PLYHP
			LAD		GR1,3
			LAD		GR2,3
			LAD		GR2,WORKBUF,GR2
			LAD		GR3,0
			CALL	DECCNV
;----
			LAD		GR1,WORKBUF
			LAD		GR2,34
			LAD		GR3,2
			LAD		GR4,6
			LAD		GR5,1
			CALL	CPYSBUF
;----
			; お金の表示
			LD		GR0,PLYMNY
			LAD		GR1,5
			LAD		GR2,WORKBUF
			LAD		GR3,0
			CALL	DECCNV
;----
			LAD		GR0,3
			LAD		GR1,HUDMNY
			LAD		GR2,5
			LAD		GR2,WORKBUF,GR2
			CALL	CPYDAT
;----
			LAD		GR1,WORKBUF
			LAD		GR2,32
			LAD		GR3,3
			LAD		GR4,8
			LAD		GR5,1
			CALL	CPYSBUF
;----
			; 操作説明の表示
			LAD		GR1,HUDMSG0
			LAD		GR2,32
			LAD		GR3,7
			LAD		GR4,8
			LAD		GR5,6
			CALL	CPYSBUF
;----
			RET
;----
HUDHP		DC	'HP:'
HUDFLR		DC	'F'
HUDMNY		DC	'00G'
HUDMSG0		DC	'移動    '
HUDMSG1		DC	'     8  '
HUDMSG2		DC	'   4   6'
HUDMSG3		DC	'     2  '
HUDMSG4		DC	'        '
HUDMSG5		DC	'階段 5  '
;----
;----
;**************************************
;  階段プログラム
;----
;----
;======================================
; 階段の設定
;======================================
ELVSET		LD		GR0,MSKELV
			CALL	MAPRDADD
			ST		GR5,ELVX
			ST		GR6,ELVY
			RET
;----
;----
;======================================
; 階段の描画
;======================================
ELVDRW		LD		GR0,CHRELV
			LD		GR5,ELVX
			LD		GR6,ELVY
			CALL	PUTSBUF
			RET
;----
;----
;======================================
;  階段の判定
; return :  GR0  0:階段なし  1:階段降り
;======================================
ELVCHK		LAD		GR0,0
			LD		GR1,INBUFL
			JZE		ELVCHKED
;----
			LD		GR1,INBUF
			CPA		GR1,='5'
			JNZ		ELVCHKED
;----
			LD		GR5,PLYX
			LD		GR6,PLYY
			CALL	MAPPEEK
			AND		GR7,MSKELV
			CPA		GR7,MSKELV
			JNZ		ELVCHKED
;----
			LAD		GR0,1
;----
ELVCHKED	RET
;----
;----
;**************************************
;  回復プログラム
;----
;----
;======================================
;  回復の設定
;======================================
CURSET		LD		GR0,MSKCUR
			LAD		GR1,0
CURSET0		CALL	MAPRDADD
			ST		GR5,CURX,GR1
			ST		GR6,CURY,GR1
			LAD		GR1,1,GR1
			CPA		GR1,CURNUM
			JMI		CURSET0
;----
			RET
;----
CURX		DS		5
CURY		DS		5
CURNUM		DC		2
CURVAL		DC		8
;----
;----
;======================================
;  回復の判定
;======================================
CURCHK		LD		GR5,PLYX
			LD		GR6,PLYY
			CALL	MAPPEEK
			AND		GR7,MSKCUR
			JZE		CURCHKE
;----
			LD		GR1,PLYHP
			ADDL	GR1,CURVAL
			CPA		GR1,PLYHPF
			JMI		CURCHK0
;----
			LD		GR1,PLYHPF
;----
CURCHK0		CPA		GR1,PLYHP
			JZE		CURCHK1
;----
			LD		GR2,GR1
			SUBL	GR2,PLYHP
			ST		GR2,PLYCUR
;----
CURCHK1		ST		GR1,PLYHP
;----
			LD		GR0,MSKCUR
			CALL	MAPRMV
;----
CURCHKE		RET
;----
;----
;======================================
; 回復の描画
;======================================
CURDRW		LD		GR0,CHRCUR
			LAD		GR1,0
;----
CURDRW0		LD		GR5,CURX,GR1
			LD		GR6,CURY,GR1
			CALL	MAPPEEK
			AND		GR7,MSKCUR
			JZE		CURDRW1
;----
			CALL	PUTSBUF
;----
CURDRW1		LAD		GR1,1,GR1
			CPA		GR1,CURNUM
			JMI		CURDRW0
;----
			RET
;----
;----
;**************************************
;  お金プログラム
;----
;----
;======================================
; お金の設定
;======================================
MNYSET		LD		GR0,MSKMNY
			LAD		GR1,0
MNYSET0		CALL	MAPRDADD
			ST		GR5,MNYX,GR1
			ST		GR6,MNYY,GR1
			LAD		GR1,1,GR1
			CPA		GR1,MNYNUM
			JMI		MNYSET0
;----
			RET
;----
MNYX		DS		5
MNYY		DS		5
MNYNUM		DC		5
;----
;----
;======================================
;  お金の判定
;======================================
MNYCHK		LD		GR5,PLYX
			LD		GR6,PLYY
			CALL	MAPPEEK
			AND		GR7,MSKMNY
			JZE		MNYCHKE
;----
			LD		GR1,PLYFMNY
			LAD		GR1,1,GR1
			ST		GR1,PLYFMNY
;----
			LD		GR1,PLYMNY
			LAD		GR1,1,GR1
			ST		GR1,PLYMNY
;----
			LD		GR0,MSKMNY
			CALL	MAPRMV
;----
MNYCHKE		RET
;----
;----
;======================================
; お金の描画
;======================================
MNYDRW		LD		GR0,CHRMNY
			LAD		GR1,0
;----
MNYDRW0		LD		GR5,MNYX,GR1
			LD		GR6,MNYY,GR1
			CALL	MAPPEEK
			AND		GR7,MSKMNY
			JZE		MNYDRW1
;----
			CALL	PUTSBUF
;----
MNYDRW1		LAD		GR1,1,GR1
			CPA		GR1,MNYNUM
			JMI		MNYDRW0
;----
			RET
;----
;----
;**************************************
;  敵プログラム
;----
;----
;======================================
;  敵座標の設定
;======================================
EMYSET		NOP
			LAD		GR1,0
;----
			; 位置を設定
EMYSET0		LD		GR0,EMYDAT,GR1
			CALL	MAPRDADD
			ST		GR5,EMYX,GR1
			ST		GR6,EMYY,GR1
;----
			; 方向を設定
			CALL	RND
			AND		GR7,=#0003
			ST		GR7,EMYDR,GR1
;----
			; HPを設定
			LD		GR0,EMYHPF
			ST		GR0,EMYHP,GR1
;----
			LAD		GR1,1,GR1
			CPA		GR1,EMYNUM
			JNZ		EMYSET0
			RET
;----
EMYNUM		DC		10			; 敵数
EMYX		DS		10			; X座標
EMYY		DS		10			; Y座標
EMYDR		DS		10			; 方向
EMYHP		DS		10			; HP
EMYHPF		DC		3			; 初期HP
; マップデータに書き込む値
EMYDAT		DC		#0010,#0020,#0030,#0040,#0050
EMYDAT2		DC		#0060,#0070,#0080,#0090,#00A0
;----
;----
;======================================
;  敵の更新
;======================================
EMYUPD		LAD		GR1,0
EMYUPD0		LD		GR0,EMYHP,GR1
			JZE		EMYUPD3
;----
			; プレイヤーが隣にいるか
			LD		GR0,MSKPLY
			LD		GR3,EMYX,GR1
			LD		GR4,EMYY,GR1
			CALL	MAPNEAR
			OR		GR7,GR7
			JZE		EMYUPD4
;----
			; 攻撃処理
			LAD		GR0,3
			CALL	PLYDMG
			JUMP	EMYUPD3
;----
			; 移動処理
EMYUPD4		LD		GR0,EMYDAT,GR1
			LD		GR5,EMYX,GR1
			LD		GR6,EMYY,GR1
			CALL	MAPRMV
;----
			LD		GR2,EMYDR,GR1
			ADDA	GR5,EMYMOVX,GR2
			ADDA	GR6,EMYMOVY,GR2
			CALL	MAPPEEK
			AND		GR7,MSKCLO
			JZE		EMYUPD1
;----
			; 直進できないのでランダムで
			; 方向を変更
			CALL	RND
			AND		GR7,=#0003
			ST		GR7,EMYDR,GR1
			JUMP	EMYUPD2
;----
			; 移動した座標を設定
EMYUPD1		ST		GR5,EMYX,GR1
			ST		GR6,EMYY,GR1
;----
			; マップデータを更新
EMYUPD2		LD		GR0,EMYDAT,GR1
			LD		GR5,EMYX,GR1
			LD		GR6,EMYY,GR1
			CALL	MAPADD
;----
EMYUPD3		LAD		GR1,1,GR1
			CPA		GR1,EMYNUM
			JMI		EMYUPD0
			RET
;----
EMYMOVX		DC	-1,1,0,0
EMYMOVY		DC	0,0,-1,1
;----
;----
;======================================
;  敵にダメージを与える
;  input :  GR0  ダメージ
;           GR1  ID
;======================================
EMYDMG		LD		GR2,EMYHP,GR1
			JZE		EMYDMGE
;----
			ST		GR1,PLYTRGE
			LD		GR2,PLYTRGED
			ADDL	GR2,GR0
			ST		GR2,PLYTRGED
;----
			LD		GR2,EMYHP,GR1
			SUBA	GR2,GR0
			ST		GR2,EMYHP,GR1
			JPL		EMYDMGE
;----
			; 死亡処理
EMYDMG0		LD		GR0,EMYDAT,GR1
			LD		GR5,EMYX,GR1
			LD		GR6,EMYY,GR1
			CALL	MAPRMV
			LAD		GR2,0
			ST		GR2,EMYHP,GR1
			LAD		GR2,-1
			ST		GR2,PLYTRGED
;----
EMYDMGE		RET
;----
;----
;======================================
;  敵の描画
;======================================
EMYDRW		NOP
			LAD		GR1,0
;----
EMYDRW0		LD		GR0,EMYHP,GR1
			JZE		EMYDRW1
;----
			LD		GR0,EMYCHRS,GR1
			LD		GR5,EMYX,GR1
			LD		GR6,EMYY,GR1
			CALL	PUTSBUF
;----
EMYDRW1		LAD		GR1,1,GR1
			CPA		GR1,EMYNUM
			JNZ		EMYDRW0
			RET
;----
EMYCHRS		DC		'a','b','c','d','e'
EMYCHRS2	DC		'f','g','h','i','j'
;----
;----
;**************************************
;  プレイヤープログラム
;----
;----
;======================================
; プレイヤー座標の設定
;======================================
PLYSET		NOP
			; 乱数を取得
PLYSETL		CALL	RND
			LD		GR5,GR7
			CALL	RND
			LD		GR6,GR7
			; 乱数の範囲を制限
			AND		GR5,PLYMSK
			AND		GR6,PLYMSK
			; 乱数にオフセットを加える
			ADDA	GR5,PLYOFFX
			ADDA	GR6,PLYOFFY
			; 迷路のデータを取得し壁だったら
			; もう一度ランダムを取得
			CALL	MAPPEEK
			OR		GR7,GR7
			JNZ		PLYSETL
			; 座標を設定
			ST		GR5,PLYX
			ST		GR6,PLYY
			LD		GR0,MSKPLY
			CALL	MAPADD
			RET
;----
PLYMSK		DC	#0007
PLYOFFX		DC	4
PLYOFFY		DC	2
;----
;----
;======================================
; プレイヤーの更新
;======================================
PLYUPD		LD		GR0,INBUFL
			JZE		PLYUPDE
			LD		GR0,PLYHP
			JZE		PLYUPDE
			; マップデータを消去
			LD		GR0,MSKPLY
			LD		GR5,PLYX
			LD		GR6,PLYY
			CALL	MAPRMV
			; 入力によって移動方向を変える
			LD		GR0,INBUF
			CPA		GR0,='4'
			JZE		PLYUPD44
			CPA		GR0,='6'
			JZE		PLYUPD66
			CPA		GR0,='8'
			JZE		PLYUPD88
			CPA		GR0,='2'
			JZE		PLYUPD22
			JUMP	PLYUPD0
;----
PLYUPD44	SUBA	GR5,=1
			JUMP	PLYUPD0
;----
PLYUPD66	ADDA	GR5,=1
			JUMP	PLYUPD0
;----
PLYUPD88	SUBA	GR6,=1
			JUMP	PLYUPD0
;----
PLYUPD22	ADDA	GR6,=1
			JUMP	PLYUPD0
;----
			; 移動先によって処理を変える
PLYUPD0		CALL	MAPPEEK
			LD		GR0,GR7
			; 壁なら移動処理をしない
			AND		GR7,MSKWAL
			JNZ		PLYUPD2
;----
			; 敵でないなら移動処理
			LD		GR7,GR0
			AND		GR7,MSKEMY
			JZE		PLYUPD1
;----
			; 敵ならダメージを与えて移動処理をしない
			SRL		GR7,4
			SUBL	GR7,=1
			LAD		GR0,1
			LD		GR1,GR7
			CALL	EMYDMG
			JUMP	PLYUPD2
;----
			; 移動処理
PLYUPD1		ST		GR5,PLYX
			ST		GR6,PLYY
;----
			; マップデータを設定
PLYUPD2		LD		GR0,MSKPLY
			LD		GR5,PLYX
			LD		GR6,PLYY
			CALL	MAPADD
;----
PLYUPDE		RET
;----
;----
;======================================
;  プレイヤーにダメージを与える
;  input :  GR0  ダメージ
;======================================
PLYDMG		LD		GR2,PLYHP
			JZE		PLYDMGE
;----
			LD		GR2,PLYD
			ADDL	GR2,GR0
			ST		GR2,PLYD
;----
			LD		GR2,PLYHP
			SUBA	GR2,GR0
			ST		GR2,PLYHP
			JPL		PLYDMGE
;----
			; 死亡処理
			LD		GR2,MSKPLY
			LD		GR5,PLYX
			LD		GR6,PLYY
			CALL	MAPRMV
			LAD		GR2,0
			ST		GR2,PLYHP
			LAD		GR2,-1
			ST		GR2,PLYD
;----
PLYDMGE		RET
;----
;----
;======================================
; プレイヤーの描画
;======================================
PLYDRW		LD		GR0,PLYHP
			JZE		PLYDRWE
			LD		GR0,CHRPLY
			LD		GR5,PLYX
			LD		GR6,PLYY
			CALL	PUTSBUF
PLYDRWE		RET
;----
;----
;**************************************
; マッププログラム
;----
;----
;======================================
; マップデータクリア
;======================================
MAPCLR		NOP
			LAD		GR0,0
			LAD		GR1,0
;----
MAPCLR0		LAD		GR2,0
			LD		GR3,MAPDATA,GR1
;----
MAPCLR1		ST		GR0,0,GR3
			LAD		GR3,1,GR3
			LAD		GR2,1,GR2
			CPA		GR2,MAPDATL
			JMI		MAPCLR1
;----
			LAD		GR1,1,GR1
			CPA		GR1,MAPDATNM
			JMI		MAPCLR0
;----
			RET
;----
;----
;======================================
; マップデータに迷路データを設定
;======================================
MAPMEZ		NOP
			LAD		GR1,0
;----
MAPMEZ0		LAD		GR2,0
			LD		GR3,MAPDATA,GR1
			LD		GR4,MEZDAT,GR1
;----
MAPMEZ1		LD		GR0,0,GR4
			CPA		GR0,CHRWAL
			JNZ		MAPMEZNX
;----
			LD		GR0,0,GR3
			OR		GR0,MSKWAL
			ST		GR0,0,GR3
;----
MAPMEZNX	LAD		GR4,1,GR4
			LAD		GR3,1,GR3
			LAD		GR2,1,GR2
			CPA		GR2,MEZLLEN
			JMI		MAPMEZ1
;----
			LAD		GR1,1,GR1
			CPA		GR1,MEZLNUM
			JMI		MAPMEZ0
;----
			RET
;----
;----
;======================================
; マップデータの取得
; input  : GR5  X座標
;          GR6  Y座標
; return : GR7  データ
;======================================
MAPPEEK		LD		GR7,MAPDATA,GR6
			ADDA	GR7,GR5
			LD		GR7,0,GR7
			RET
;----
;----
;======================================
; マップデータの近傍を調べる
; input  : GR0  データ
;          GR3  X座標
;          GR4  Y座標
; return : GR7  0:なし  0以外:データ
;======================================
MAPNEAR		NOP
			LD		GR6,GR4
;----
			; 左隣を取得
			LD		GR5,GR3
			SUBL	GR5,=1
			CALL	MAPPEEK
			AND		GR7,GR0
			JNZ		MAPNEARE
;----
			; 右隣を取得
			LD		GR5,GR3
			ADDL	GR5,=1
			CALL	MAPPEEK
			AND		GR7,GR0
			JNZ		MAPNEARE
;----
			LD		GR5,GR3
;----
			; 上隣を取得
			LD		GR6,GR4
			SUBL	GR6,=1
			CALL	MAPPEEK
			AND		GR7,GR0
			JNZ		MAPNEARE
;----
			; 下隣を取得
			LD		GR6,GR4
			ADDL	GR6,=1
			CALL	MAPPEEK
			AND		GR7,GR0
			JNZ		MAPNEARE
;----
			LAD		GR7,0
;----
MAPNEARE	RET
;----
;----
;======================================
; マップデータの追加
; input  : GR0  データ
;          GR5  X座標
;          GR6  Y座標
; work   : GR7
;======================================
MAPADD		LD		GR7,MAPDATA,GR6
			ADDA	GR7,GR5
			OR		GR0,0,GR7
			ST		GR0,0,GR7
			RET
;----
;----
;======================================
; マップデータの削除
; input  : GR0  データ
;          GR5  X座標
;          GR6  Y座標
; work   : GR7
;======================================
MAPRMV		LD		GR7,MAPDATA,GR6
			ADDA	GR7,GR5
			XOR		GR0,0,GR7
			ST		GR0,0,GR7
			RET
;----
;----
;======================================
; マップデータにランダムで追加
; input  : GR0  データ
; return : GR5  X座標
;          GR6  Y座標
;======================================
MAPRDADD	NOP
			; 乱数を取得
MAPRDAD0	CALL	RND
			LD		GR5,GR7
			CALL	RND
			LD		GR6,GR7
			; 乱数の範囲を制限
			AND		GR5,=#001F
			AND		GR6,=#000F
			; 範囲外だったらもう一度ランダムを取得
			CPA		GR5,MEZLLEN
			JPL		MAPRDAD0
			JZE		MAPRDAD0
			CPA		GR6,MEZLNUM
			JPL		MAPRDAD0
			JZE		MAPRDAD0
			; マップデータを取得し既に何か設置されていたら
			; もう一度ランダムを取得
			CALL	MAPPEEK
			OR		GR7,GR7
			JNZ		MAPRDAD0
			; 座標を設定
			CALL	MAPADD
			RET
;----
;----
;**************************************
; 迷路プログラム
;----
;----
;======================================
; 迷路生成
;======================================
MEZCRT		CALL	MEZCLR
;----
			LD		GR0,CHRWAL
;----
			; 外壁を埋める
			; 上下壁
			LD		GR1,MEZLLEN
MEZCRTL0	SUBL	GR1,=1
			ST		GR0,MEZL00,GR1
			ST		GR0,MEZL12,GR1
			JNZ		MEZCRTL0
;----
			; 左右壁
			LD		GR1,MEZLNUM
			LAD		GR2,0
MEZCRTL1	ST		GR0,MEZL00,GR2
			ST		GR0,MEZL00R,GR2
			ADDL	GR2,MEZLLEN
			SUBL	GR1,=1
			JNZ		MEZCRTL1
;----
			; 棒倒し法で壁を生成
;----
			;----------------
			; 一段目は上も倒す
			LD		GR1,MEZSTR0
MEZCRTL2	NOP
			; 既に壁があったら処理を飛ばす
			LD		GR2,0,GR1
			CPA		GR2,CLRCHR
			JNZ		MEZCRNX0
;----
			; 壁を設定
			ST		GR0,0,GR1
;----
			; 壁の無いマスが出るまでランダムを取得する
RNDL0		CALL	RND
			AND		GR7,=#0003
			LD		GR7,PICH,GR7
			ADDA	GR7,GR1
			LD		GR2,0,GR7
			CPA		GR2,CLRCHR
			JNZ		RNDL0
;----
			; 棒倒し壁を設定
			ST		GR0,0,GR7
;----
MEZCRNX0	LAD		GR1,2,GR1
			CPA		GR1,MEZEND0
			JMI		MEZCRTL2
;----
;----
			;----------------
			; 二段目以降は上以外に壁を設定
			LD		GR1,MEZSTR1
MEZCRTL3	LD		GR2,0
MEZCRTL4	NOP
;----
			; 既に壁があったら処理を飛ばす
			LD		GR3,0,GR1
			CPA		GR3,CLRCHR
			JNZ		MEZCRNX1
;----
			; 壁を設定
			ST		GR0,0,GR1
;----
			; 壁が無くて上以外のマスが出るまでランダムを取得する
RNDL1		CALL	RND
			; 上ならもう一度ランダム取得
			AND		GR7,=#0003
			JZE		RNDL1
			; 既に壁があったらもう一度ランダム取得
			LD		GR7,PICH,GR7
			ADDA	GR7,GR1
			LD		GR3,0,GR7
			CPA		GR3,CLRCHR
			JNZ		RNDL1
;----
			; 棒倒し壁を設定
			ST		GR0,0,GR7
;----
MEZCRNX1	LAD		GR1,2,GR1
			LAD		GR2,2,GR2
			CPA		GR2,MEZLLEN
			JMI		MEZCRTL4
;----
			ADDA	GR1,MEZLLEN
			LAD		GR1,-1,GR1
;			AND		GR1,=#FFFE
			CPA		GR1,MEZEND1
			JMI		MEZCRTL3
;----
			; ランダムに壁に穴をあける
			LD		GR0,CLRCHR
			LAD		GR1,0
;----
MEZCRT5		CALL	RND
			AND		GR7,=#001F
			ADDL	GR7,=1
			CPL		GR7,=29
			JPL		MEZCRT5
			LD		GR5,GR7
;----
MEZCRT6		CALL	RND
			AND		GR7,=#000F
			ADDA	GR7,=1
			CPA		GR7,=11
			JPL		MEZCRT6
			LD		GR6,GR7
;----
			LD		GR7,MEZDAT,GR6
			ADDL	GR7,GR5
			ST		GR0,0,GR7
;----
			LAD		GR1,1,GR1
			CPA		GR1,MEZDIGNM
			JMI		MEZCRT5
;----
			RET
;----
PICH		DC	 -31,-1,1,31
;----
;----
;======================================
; 迷路クリア
;======================================
MEZCLR		LD		GR0,CLRCHR
			LD		GR1,MEZDATSZ
MEZCLRL		SUBL	GR1,=1
			ST		GR0,MEZL00,GR1
			JNZ		MEZCLRL
			RET
MEZCLRD		DC	' '
;----
;----
;======================================
; 迷路描画
;======================================
MEZDRW		LAD		GR1,0
;----
MEZDRWL0	LAD		GR2,0
			LD		GR3,MEZDAT,GR1
			LD		GR4,SCRBUF,GR1
;----
MEZDRWL1	LD		GR0,0,GR3
			ST		GR0,0,GR4
;----
			LAD		GR3,1,GR3
			LAD		GR4,1,GR4
			LAD		GR2,1,GR2
			CPA		GR2,MEZLLEN
			JMI		MEZDRWL1
;----
			LAD		GR1,1,GR1
			CPA		GR1,MEZLNUM
			JMI		MEZDRWL0
			RET
;----
;----
;======================================
; 迷路データ
;======================================
MEZL00		DS	30		; 上壁,左壁開始
MEZL00R		DS	1		; 右壁開始
MEZL01		DS	31		; 空白行
MEZL02		DS	31		; 1段目柱行
MEZL03		DS	31		; 空白行
MEZL04		DS	31		; 2段目柱行
MEZL05		DS	31		; 空白行
MEZL06		DS	31		; 柱行
MEZL07		DS	31		; 空白行
MEZL08		DS	31		; 柱行
MEZL09		DS	31		; 空白行
MEZL10		DS	31		; 柱行
MEZL11		DS	31		; 空白行
MEZL12		DS	31		; 下壁
MEZDAT		DC	MEZL00,MEZL01,MEZL02,MEZL03,MEZL04
MEZDAT1		DC	MEZL05,MEZL06,MEZL07,MEZL08,MEZL09
MEZDAT2		DC	MEZL10,MEZL11,MEZL12
;----
MEZLLEN		DC	31
MEZLNUM		DC	13
MEZDATSZ	DC	403
;----
; 一段目の開始アドレスと終了アドレス
MEZSTR0		DC	MEZL02
MEZEND0		DC	MEZL03
;----
; 二段目以降の開始アドレスと終了アドレス
MEZSTR1		DC	MEZL04
MEZEND1		DC	MEZL11
;----
MEZDIGNM	DC	20		; 穴をあける壁の数
;----
;----
;**************************************
; ユーティリティモジュール
;----
;----
;======================================
; 10進数変換関数
; input  : GR0   数値
;        : GR1   桁数 最大5桁
;        : GR2   格納アドレス
;        : GR3   0:ゼロサプレス,1:ゼロ埋め
;======================================
DECCNV		NOP
			; 桁数を退避
			PUSH	0,GR1
			; 格納アドレスを退避
			PUSH	0,GR2
			; 桁数から処理回数を設定
			LD		GR6,MAXDIGIT
			SUBL	GR6,GR1
			; 数値を初期化
			LD		GR4,GR0
;----
DECCNV0		LAD		GR5,0
DECCNV1		CPL		GR4,DIGIT,GR6
			JMI		DECCNV1E
;----
			SUBL	GR4,DIGIT,GR6
			LAD		GR5,1,GR5
			JUMP	DECCNV1
;----
DECCNV1E	LD		GR7,NUMCHR,GR5
			ST		GR7,0,GR2
			LAD		GR2,1,GR2
			LAD		GR6,1,GR6
;----
			CPL		GR6,MAXDIGIT
			JNZ		DECCNV0
;----
			; ゼロサプレス処理
			POP		GR2
			POP		GR1
			OR		GR3,GR3
			JNZ		DECCNVED
;----
DECCNV2		LD		GR7,0,GR2
			CPA		GR7,NUMCHR
			JNZ		DECCNVED
;----
			SUBL	GR1,=1
			JZE		DECCNVED
;----
			LD		GR6,PRSSCHR
			ST		GR6,0,GR2
			LAD		GR2,1,GR2
			JUMP	DECCNV2
;----
DECCNVED	RET
;----
;----
PRSSCHR		DC	' '
NUMCHR		DC	'0','1','2','3','4'
NUMCHR2		DC	'5','6','7','8','9'
DIGIT		DC	10000,1000,100,10,1
MAXDIGIT	DC	5
DIGLOPNM	DC	4
;----
;----
;======================================
; ランダム関数
; input  : なし
; return : GR7   乱数
; work   : GR6
;======================================
RND			LD		GR7,SEED
			LD		GR6,GR7
			SLL		GR7,2
			ADDL	GR7,GR6
			ADDL	GR7,=#3711
			ST		GR7,SEED
			SRL		GR7,4
			AND		GR7,=#00FF
			RET
;----
SEED		DC	#3643
;----
;----
;======================================
; データコピー関数
; input  : GR0  長さ
;          GR1  コピー元アドレス
;          GR2  コピー先アドレス
;======================================
CPYDAT		LD		GR3,GR0
CPYDAT0		LD		GR4,0,GR1
			ST		GR4,0,GR2
			LAD		GR1,1,GR1
			LAD		GR2,1,GR2
			SUBL	GR3,=1
			JNZ		CPYDAT0
			RET
;----
;----
; プログラム終了
;**************************************
			END
